plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.mozilla.rust-android-gradle.rust-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.23'
}

def configFile = 'config.properties'
Properties config = new Properties()
config.load(project.rootProject.file(configFile).newDataInputStream())

if (config.isEmpty())
    throw new Exception("Failed to load ${configFile} file, create one first!")
def cargoProfile = config.getProperty('cargo.profile', 'release')
def attestatorPath = config.getProperty('cargo.attestatorPath')
def strongboxEnabled = config.getProperty('build.strongboxEnabled', 'false')
def verifyStateHash = config.getProperty('build.verifyStateHash', 'false')
def writeStateHash = config.getProperty('build.writeStateHash', 'false')
def buildType = gradle.startParameter.taskNames.any { it.contains("Debug") } ? "debug" : "release"

def ksPath = config.getProperty('ks.path')
def ksPassword = config.getProperty('ks.password')
def ksAlias = config.getProperty('ks.alias')

println(buildType)
android {
    namespace 'proofcastlabs.tee'
    compileSdk rootProject.ext.targetSdk

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "proofcastlabs.tee";
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        ndk.abiFilters rootProject.ext.ndkAbiFilters
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile buildType == "release" ? file(ksPath) : null
            storePassword ksPassword
            keyAlias ksAlias
            keyPassword ksPassword
        }
    }

    buildTypes {
        release {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    applicationVariants.all { variant ->
        buildConfigField('String', 'STRONGBOX_ENABLED', "\"${strongboxEnabled}\"")
        buildConfigField('String', 'VERIFY_STATE_HASH', "\"${verifyStateHash}\"")
        buildConfigField('String', 'WRITE_STATE_HASH', "\"${writeStateHash}\"")
    }


    gradle.buildFinished {
        if (buildType == 'release') {
            println("Signed with $ksPath")
        }
        copy {
            def fileName = "app-${buildType}.apk"
            def origin = "build/outputs/apk/${buildType}/${fileName}"
            def dest = "${rootProject.name}-${buildType}.apk"
            println "Copying from $origin to $dest"
            from origin
            rename fileName, dest
            into "../"
        }
    }


}

cargo {
    // Leave it for arm32, because artifacts for arm64
    // have not been built for libshathree. If want to use
    // arm64, build shathree for arm64 first. Analyze apk
    // to check the library is included. Also
    //   ./gradlew -d clean assembleDebug
    // and uncomment the following line to see if the
    // libraries are compiled for the correct arch
    //    verbose = true
    targets = [ "arm" ]
    profile = cargoProfile;
    libname = "strongbox"
    targetIncludes = ['libstrongbox.so']
    targetDirectory = "${attestatorPath}/target"
    module  = "${attestatorPath}/apps/strongbox"
}

tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

def ktorVersion = '2.3.12'
dependencies {
    implementation project(path: ':sqlite3')
    implementation 'com.google.android.gms:play-services-safetynet:18.0.1'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-cbor:1.6.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.14.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "io.ktor:ktor-client-core:$ktorVersion"
    implementation "io.ktor:ktor-client-cio:$ktorVersion"
    implementation "io.ktor:ktor-client-websockets:$ktorVersion"
    implementation "io.ktor:ktor-client-okhttp:$ktorVersion"
    implementation 'commons-codec:commons-codec:1.17.0'
    implementation 'org.web3j:core:4.8.7-android'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}